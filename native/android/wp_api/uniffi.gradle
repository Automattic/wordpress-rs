// Adopted from https://github.com/mozilla/application-services/blob/v120.0.1/publish.gradle#L206-L226
//
// A convenience function for configuring a `uniffi-bindgen` task,
// with appropriate dependency info. This will call `uniffi-bindgen`
// for the provided `.dylib` file in order to generate Kotlin language
// bindings and include them in the source set for the project.
ext.configureUniFFIBindgen = { dylibFilePath ->
    android.libraryVariants.all { variant ->
        def uniffiGeneratedPath = "generated/source/uniffi/${variant.name}/java"
        def t = tasks.register("generate${variant.name.capitalize()}UniFFIBindings", Exec) {
            workingDir project.rootDir
            commandLine 'cargo', 'run', '--release', '--bin', 'uniffi_bindgen', 'generate', '--library', "${project.projectDir}/${dylibFilePath}", '--out-dir', "${buildDir}/${uniffiGeneratedPath}", '--language', 'kotlin'
            outputs.dir "${buildDir}/${uniffiGeneratedPath}"
            // Re-generate if the interface definition changes.
            inputs.file "${project.projectDir}/${dylibFilePath}"
            // Re-generate if our uniffi-bindgen tooling changes.
            inputs.dir "${project.rootDir}/../../uniffi_bindgen/"
            // Re-generate if our uniffi-bindgen version changes.
            inputs.file "${project.rootDir}/../../Cargo.lock"
        }
        variant.registerJavaGeneratingTask(t.get(), new File(buildDir, uniffiGeneratedPath))
    }
}
