plugins {
    id("com.android.library")
    id("org.jetbrains.kotlin.android")
    id("org.mozilla.rust-android-gradle.rust-android")
    id("com.automattic.android.publish-to-s3")
}

apply from: '../common.gradle'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

project.ext.testCredentials = ext.readTestCredentialsFromFile()

android {
    namespace = "rs.wordpress.wp_api"

    compileSdk = 34

    defaultConfig {
        minSdk = 24

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        buildConfig = true
    }

    buildTypes {
        debug {
            buildConfigField("String", "TEST_SITE_URL", "\"${project.ext.testCredentials.siteUrl}\"")
            buildConfigField("String", "TEST_ADMIN_USERNAME", "\"${project.ext.testCredentials.adminUsername}\"")
            buildConfigField("String", "TEST_ADMIN_PASSWORD", "\"${project.ext.testCredentials.adminPassword}\"")
            buildConfigField("String", "TEST_SUBSCRIBER_USERNAME", "\"${project.ext.testCredentials.subscriberUsername}\"")
            buildConfigField("String", "TEST_SUBSCRIBER_PASSWORD", "\"${project.ext.testCredentials.subscriberPassword}\"")
        }
    }

    lintOptions {
        // There is an incorrect lint error in generated wp_api.kt about the usage of NewApi
        // that's related to the usage of `android.system.SystemCleaner`.
        //
        // At the time of this comment, generated bindings only use this `SystemCleaner` for
        // API's above 33 and fallback to Jna cleaner `UniffiJnaCleaner` that's available for
        // earlier APIs.
        //
        // Instead of completely ignoring this issue, we are tracking it through the baseline lint
        // file - at least for now.
        baseline file("${project.rootDir}/config/lint/baseline.xml")
    }

    sourceSets {
        androidTest.jniLibs.srcDirs += "$buildDir/rustJniLibs/android"
    }
}

repositories {
    mavenCentral()
    google()
}

dependencies {
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation "androidx.annotation:annotation:$androidxAnnotationVersion"
    implementation "net.java.dev.jna:jna:$jnaVersion@aar"

    androidTestImplementation "androidx.test:runner:$androidxTestRunnerVersion"
    androidTestImplementation "androidx.test:rules:$androidxTestRulesVersion"
    androidTestImplementation "junit:junit:$junitVersion"
    androidTestImplementation 'org.jetbrains.kotlin:kotlin-test'
    androidTestImplementation "net.java.dev.jna:jna:$jnaVersion@aar"

    testImplementation "junit:junit:$junitVersion"
    testImplementation "net.java.dev.jna:jna:$jnaVersion"
}

ext.setupCargo("wp_api", ext.getNativeRustTarget(com.sun.jna.Platform.RESOURCE_PREFIX))
ext.configureUniFFIBindgen("wp_api")

project.afterEvaluate {
    publishing {
        publications {
            MavenPublication(MavenPublication) {
                from components.release

                groupId "rs.wordpress"
                artifactId "wp_api"
                // version is set by 'publish-to-s3' plugin
            }
        }
   }
}

