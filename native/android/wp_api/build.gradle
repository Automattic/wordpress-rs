plugins {
    id("com.android.library")
    id("org.jetbrains.kotlin.android")
    id("org.mozilla.rust-android-gradle.rust-android")
    id("com.automattic.android.publish-to-s3")
}

apply from: '../common.gradle'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

android {
    namespace = "rs.wordpress.wp_api"

    compileSdk = 34

    defaultConfig {
        minSdk = 24

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }

    lintOptions {
        // There is an incorrect lint error in generated wp_api.kt about the usage of NewApi
        // that's related to the usage of `android.system.SystemCleaner`.
        //
        // At the time of this comment, generated bindings only use this `SystemCleaner` for
        // API's above 33 and fallback to Jna cleaner `UniffiJnaCleaner` that's available for
        // earlier APIs.
        //
        // Instead of completely ignoring this issue, we are tracking it through the baseline lint
        // file - at least for now.
        baseline file("${project.rootDir}/config/lint/baseline.xml")
    }

    sourceSets {
        androidTest.jniLibs.srcDirs += "$buildDir/rustJniLibs/android"
    }
}

repositories {
    mavenCentral()
    google()
}

dependencies {
    implementation("com.squareup.okhttp3:okhttp:4.12.0")
    implementation "androidx.annotation:annotation:1.7.1"

    androidTestImplementation "androidx.test:runner:1.4.0"
    androidTestImplementation "androidx.test:rules:1.4.0"
    androidTestImplementation "junit:junit:4.13.2"
    androidTestImplementation 'org.jetbrains.kotlin:kotlin-test'

    testImplementation "junit:junit:4.13.2"
}

ext.setupCargo("wp_api", ext.getNativeRustTarget(com.sun.jna.Platform.RESOURCE_PREFIX))
ext.configureUniFFIBindgen("wp_api")
ext.addJnaDependencies()

project.afterEvaluate {
    publishing {
        publications {
            MavenPublication(MavenPublication) {
                from components.release

                groupId "rs.wordpress"
                artifactId "wp_api"
                // version is set by 'publish-to-s3' plugin
            }
        }
   }
}

