plugins {
    id("com.android.library")
    id("org.jetbrains.kotlin.android")
    id("org.mozilla.rust-android-gradle.rust-android")
    id("com.automattic.android.publish-to-s3")
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

android {
    namespace = "org.wordpress.rs"

    compileSdk = 33

    defaultConfig {
        minSdk = 24

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main.java.srcDirs += "$buildDir/generated/source"
        androidTest.jniLibs.srcDirs += "$buildDir/rustJniLibs/android"
    }
}

repositories {
    mavenCentral()
    google()
}

dependencies {
    implementation "net.java.dev.jna:jna:5.13.0@aar"
    androidTestImplementation "net.java.dev.jna:jna:5.13.0@aar"

    androidTestImplementation "androidx.test:runner:1.4.0"
    androidTestImplementation "androidx.test:rules:1.4.0"
    androidTestImplementation "junit:junit:4.13.2"

    testImplementation "junit:junit:4.13.2"
    testImplementation "net.java.dev.jna:jna:5.13.0"
}

cargo {
    module  = "../../../wordpress_api/"
    libname = "wordpress_api"
    profile = "release"
    targets = ["arm", "arm64", "x86", "x86_64", "darwin-aarch64"]
    targetDirectory = '../../../target'
}

project.afterEvaluate {
    publishing {
        publications {
            MavenPublication(MavenPublication) {
                from components.release

                groupId "org.wordpress"
                artifactId "rs"
                // version is set by 'publish-to-s3' plugin
            }
        }
   }
}

tasks.matching { it.name.matches(/merge.*JniLibFolders/) }.configureEach {
    it.inputs.dir(new File(buildDir, "rustJniLibs/android"))
    it.dependsOn("cargoBuild")
}
