plugins {
    id("com.android.library")
    id("org.jetbrains.kotlin.android")
    id("org.mozilla.rust-android-gradle.rust-android")
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

android {
    namespace = "org.wordpress.rs"

    compileSdk = 33

    defaultConfig {
        minSdk = 24

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main.java.srcDirs += "$buildDir/generated/source"
        androidTest.jniLibs.srcDirs += "$buildDir/rustJniLibs/android"
    }
}

repositories {
    mavenCentral()
    google()
}

dependencies {
    implementation "net.java.dev.jna:jna:5.7.0@aar"
    testImplementation "net.java.dev.jna:jna:5.7.0@aar"
    androidTestImplementation "net.java.dev.jna:jna:5.7.0@aar"

    androidTestImplementation "androidx.test:runner:1.4.0"
    androidTestImplementation "androidx.test:rules:1.4.0"
    androidTestImplementation "junit:junit:4.13.2"

    testImplementation "junit:junit:4.13.2"
}

cargo {
    module  = "../../../wordpress_api/"
    libname = "wordpress_api"
    profile = "release"
    targets = ["arm", "arm64", "x86", "x86_64"]
    targetDirectory = '../../../target'
}

project.afterEvaluate {
    // The `cargoBuild` task isn't available until after evaluation.
    android.libraryVariants.all { variant ->
        def productFlavor = ""
        variant.productFlavors.each {
            productFlavor += "${it.name.capitalize()}"
        }
        def buildType = "${variant.buildType.name.capitalize()}"
        tasks["generate${productFlavor}${buildType}Assets"].dependsOn(tasks["cargoBuild"])
    }
}
