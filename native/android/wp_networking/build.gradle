plugins {
    id("com.android.library")
    id("org.jetbrains.kotlin.android")
    id("org.mozilla.rust-android-gradle.rust-android")
    id("com.automattic.android.publish-to-s3")
}

apply from: '../common.gradle'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

android {
    namespace = "rs.wordpress.wp_networking"

    compileSdk = 33

    defaultConfig {
        minSdk = 24

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            debugSymbolLevel 'FULL'
        }
    }

    sourceSets {
        androidTest.jniLibs.srcDirs += "$buildDir/rustJniLibs/android"
    }

    packagingOptions {
        doNotStrip "**/*.so"
    }
}

repositories {
    mavenCentral()
    google()
}

dependencies {
    implementation project(':wp_api')

    androidTestImplementation "androidx.test:runner:1.4.0"
    androidTestImplementation "androidx.test:rules:1.4.0"
    androidTestImplementation "junit:junit:4.13.2"

    testImplementation "junit:junit:4.13.2"
}

ext.setupCargo("wp_networking", ext.getNativeRustTarget(com.sun.jna.Platform.RESOURCE_PREFIX))
ext.configureUniFFIBindgen("libwp_networking.dylib")
ext.addJnaDependencies()

project.afterEvaluate {
    publishing {
        publications {
            MavenPublication(MavenPublication) {
                from components.release

                groupId "rs.wordpress"
                artifactId "wp_networking"
                // version is set by 'publish-to-s3' plugin
            }
        }
   }
}

