plugins {
    id("java-library")
    id("org.jetbrains.kotlin.jvm")
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

apply from: '../common.gradle'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

project.ext.testCredentials = ext.readTestCredentialsFromFile()

repositories {
    mavenCentral()
    google()
}

dependencies {
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation "net.java.dev.jna:jna:$jnaVersion"

    testImplementation "junit:junit:$junitVersion"
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
}

sourceSets {
    main {
        java {
            srcDir "${buildDir}/generated/source/uniffi/java/uniffi"
        }
    }
    test {
        resources {
            srcDir "${buildDir}/jniLibs/desktop"
        }
    }
}

def configureUniFFIBindgenTaskName = project.ext.configureUniFFIBindgen("wp_api")
tasks.named("compileKotlin").configure {
    dependsOn tasks.named(configureUniFFIBindgenTaskName)
}
tasks.register("copyDesktopJniLibs", Copy) {
    dependsOn tasks.named("cargoBuildLibraryRelease")
    from "$project.rootDir/../../target/release/libwp_api.dylib"
    into "${buildDir}/jniLibs/desktop"
}
tasks.named("processTestResources").configure {
    dependsOn tasks.named("copyDesktopJniLibs")
}
