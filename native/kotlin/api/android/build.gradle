plugins {
    id("com.android.library")
    id("org.jetbrains.kotlin.android")
    id("org.mozilla.rust-android-gradle.rust-android")
    id("com.automattic.android.publish-to-s3")
}

apply from: "$project.rootDir/common.gradle"

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

project.ext.testCredentials = ext.readTestCredentialsFromFile()

android {
    namespace = "rs.wordpress.api.android"

    compileSdk = libs.versions.android.compileSdk.get().toInteger()

    defaultConfig {
        minSdk = libs.versions.android.minSdk.get().toInteger()
        targetSdk = libs.versions.android.targetSdk.get().toInteger()

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        buildConfig = true
    }

    buildTypes {
        debug {
            if (project.ext.testCredentials) {
                buildConfigField("String", "TEST_SITE_URL", "\"${project.ext.testCredentials.siteUrl}\"")
                buildConfigField("String", "TEST_ADMIN_USERNAME", "\"${project.ext.testCredentials.adminUsername}\"")
                buildConfigField("String", "TEST_ADMIN_PASSWORD", "\"${project.ext.testCredentials.adminPassword}\"")
                buildConfigField("String", "TEST_SUBSCRIBER_USERNAME", "\"${project.ext.testCredentials.subscriberUsername}\"")
                buildConfigField("String", "TEST_SUBSCRIBER_PASSWORD", "\"${project.ext.testCredentials.subscriberPassword}\"")
            }
        }
    }

    lintOptions {
        // There is an incorrect lint error in generated wp_api.kt about the usage of NewApi
        // that's related to the usage of `android.system.SystemCleaner`.
        //
        // At the time of this comment, generated bindings only use this `SystemCleaner` for
        // API's above 33 and fallback to Jna cleaner `UniffiJnaCleaner` that's available for
        // earlier APIs.
        //
        // Instead of completely ignoring this issue, we are tracking it through the baseline lint
        // file - at least for now.
        baseline file("${project.rootDir}/config/lint/baseline.xml")
    }

    sourceSets {
        androidTest.jniLibs.srcDirs += "$buildDir/rustJniLibs/android"
    }
}

repositories {
    maven {
        url "https://a8c-libs.s3.amazonaws.com/android"
        content {
            includeGroup "rs.wordpress.api"
        }
    }
    mavenCentral()
    google()
}

dependencies {
    api (wpApiKotlinProjectDependency) {
        exclude group: "net.java.dev.jna"
    }
    implementation(libs.okhttp)
    implementation(libs.androidx.annotation)
    implementation(libs.jna) {
        artifact {
            type = 'aar'
        }
    }

    androidTestImplementation(libs.androidx.runner)
    androidTestImplementation(libs.androidx.rules)
    androidTestImplementation(libs.junit)
    androidTestImplementation(libs.kotlin.test)
    androidTestImplementation(libs.jna) {
        artifact {
            type = 'aar'
        }
    }
    androidTestImplementation(libs.kotlinx.coroutines.test)

    testImplementation(libs.junit)
    testImplementation(libs.jna)
}

ext.setupCargo("wp_api", ext.getNativeRustTarget(com.sun.jna.Platform.RESOURCE_PREFIX))

project.afterEvaluate {
    publishing {
        publications {
            MavenPublication(MavenPublication) {
                from components.release

                groupId "rs.wordpress.api"
                artifactId "android"
                // version is set by 'publish-to-s3' plugin
            }
        }
   }
}

