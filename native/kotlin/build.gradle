plugins {
    id("com.android.library") apply false
    id("org.jetbrains.kotlin.android") apply false
    id("io.gitlab.arturbosch.detekt") apply false
}

ext {
    okHttpVersion = '4.12.0'
    androidxAnnotationVersion = '1.7.1'
    androidxTestRunnerVersion = '1.5.2'
    androidxTestRulesVersion = '1.5.0'
    jnaVersion = '5.13.0'
    junitVersion = '4.13.2'

    cargoProjectRoot = "${project.rootDir}/../.."
    wpApiKotlinProjectDependency = project.hasProperty("wpApiKotlinVersion") ? "rs.wordpress.api:kotlin:${project.getProperty("wpApiKotlinVersion")}" : project(":api:kotlin")
}

allprojects {
    apply plugin: 'io.gitlab.arturbosch.detekt'

    detekt {
        toolVersion = gradle.ext.detektVersion
        buildUponDefaultConfig = true
        config = files("${project.rootDir}/config/detekt/detekt.yml")
        allRules = false
    }

    tasks.withType(io.gitlab.arturbosch.detekt.Detekt).configureEach {
        jvmTarget = "1.8"
        reports {
            html.required.set(true)
            xml.required.set(true)
        }

        // Exclude generated bindings
        exclude("**/wp_api.kt")
    }

    tasks.withType(io.gitlab.arturbosch.detekt.DetektCreateBaselineTask).configureEach {
        jvmTarget = "1.8"

        // Exclude generated bindings
        exclude("**/wp_api.kt")
    }

    dependencies {
        detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:$gradle.ext.detektVersion"
    }
}
